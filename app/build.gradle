buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.0.0'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'jacoco'

dependencies {
    implementation project(':libraries:beers')
    implementation group: 'com.j256.ormlite',            name: 'ormlite-core',     version: '5.0'
    implementation group: 'com.j256.ormlite',            name: 'ormlite-android',  version: '5.0'
    implementation group: 'com.google.android.material', name: 'material',         version: '1.8.0'
    implementation group: 'org.slf4j',                   name: 'slf4j-android',    version: '1.7.25'
    implementation 'androidx.multidex:multidex:2.0.1'

    // Updated AndroidX Test libraries for Android 13+ (API 33+) support
    // Versions 1.5.0+ add RECEIVER_EXPORTED flag support required by targetSdkVersion 34
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.5.1'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test:core:1.5.0'
    androidTestImplementation 'junit:junit:4.13.2'

    // Additional test dependencies
    androidTestImplementation 'androidx.legacy:legacy-support-v4:1.0.0'
    androidTestImplementation 'android.arch.core:core-testing:1.1.1'
    androidTestImplementation 'org.easymock:easymock:3.6'

    // Unit test dependencies (JVM tests in src/test/)
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.13'
    testImplementation 'org.mockito:mockito-core:5.14.2'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.12.0'
    testImplementation 'androidx.test:core:1.5.0'
    testImplementation 'androidx.test.ext:junit:1.1.5'
}

android {
    compileSdkVersion 33
    namespace 'ralcock.cbf'

    defaultConfig {
           versionCode 27
           versionName "2025.0.0.1"
           minSdkVersion 14
           targetSdkVersion 34
           multiDexEnabled true

           testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
       }

    lint {
        abortOnError false
    }

    testOptions {
        unitTests {
            includeAndroidResources = true  // Required for Robolectric
            returnDefaultValues = true
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
    }

    // See https://www.timroes.de/2013/09/22/handling-signing-configs-with-gradle/
    if(project.hasProperty("RELEASE") && new File("app/release.gradle").exists()){
        logger.quiet("Building release")
        apply from: "release.gradle";
    } else {
        logger.quiet("Building debug" )
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked'
    // Suppress deprecation warnings - deprecated APIs are documented with @SuppressWarnings and TODO comments
    options.deprecation = false
}

// JaCoCo configuration for test coverage
jacoco {
    toolVersion = "0.8.8"
}

tasks.register('jacocoTestReport', JacocoReport) {
    group = "Reporting"
    description = "Generate JaCoCo coverage reports from unit tests and instrumented tests"

    // Explicit dependencies to avoid Gradle task ordering issues
    dependsOn tasks.named('testDebugUnitTest')
    dependsOn tasks.named('testReleaseUnitTest')
    dependsOn tasks.named('compileDebugJavaWithJavac')

    // Note: This task should be run after 'createDebugCoverageReport' or 'connectedCheck'
    // It processes the .ec coverage files generated by instrumented tests

    reports {
        xml.required = true
        html.required = true
        csv.required = true
    }

    def fileFilter = [
        '**/R.class',
        '**/R$*.class',
        '**/BuildConfig.*',
        '**/Manifest*.*',
        '**/*Test*.*',
        'android/**/*.*',
        '**/AutoValue_*.*',
        '**/*JavascriptBridge.class'
    ]

    def mainSrc = "${project.projectDir}/src/main/java"

    // Define the primary location for compiled classes
    def javaClasses = fileTree(
        dir: "${buildDir}/intermediates/javac/debug/classes",
        excludes: fileFilter
    )

    sourceDirectories.setFrom(files([mainSrc]))
    classDirectories.setFrom(files([javaClasses]))

    // Collect execution data from instrumented tests (.ec files)
    executionData.setFrom(fileTree(dir: buildDir, includes: [
        'outputs/code_coverage/debugAndroidTest/connected/**/*.ec',
        'outputs/unit_test_code_coverage/debugUnitTest/testDebugUnitTest.exec',
        'jacoco/*.exec'
    ]))

    // Ensure execution data files exist before including them
    executionData = files(executionData.findAll { it.exists() })

    doFirst {
        if (executionData.isEmpty()) {
            logger.warn("No test execution data found. Run 'connectedCheck' or 'createDebugCoverageReport' first.")
        }
    }
}